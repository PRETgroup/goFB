basicFB BfbIDMTCurve;

interface of BfbIDMTCurve {
	in event tick;
    in event i_measured;
    in event iSet_change;

    out event unsafe;

    in with i_measured usint i;
    in with iSet_change usint iSet;    
} 

architecture of BfbIDMTCurve {
	internals {
        uint v := 0;
        uint thresh := 0;
        //udint K := 10000;	
		//udint B := 135;
    }
    
    states {
		s_start {
			-> s_wait;
		}

		s_wait {
			run in "ST" `v := 0;`;
			-> s_count on i > iSet;
		}

        s_count {
            run updateThresh;
            run in "ST" `v := v + 1;`;
            
            -> s_wait on i <= iSet;
            -> s_over on v > thresh;
            -> s_count on tick;
        }

        s_over {
            emit unsafe;
            -> s_wait on i <= iSet;
            -> s_over;
        }
	}

    algorithm updateThresh in "ST" `
        if i > 145 then
            thresh := 5;
        elsif i > 100 then
            thresh := 10;
        elsif i > 77 then
            thresh := 15;
        elsif i > 55 then
            thresh := 30;
        elsif i > 32 then
            thresh := 60;
        elsif i > 23 then
            thresh := 100;
        elsif i > 19 then
            thresh := 150;
        elsif i > 14 then
            thresh := 300;
        else
            thresh := 400;
        end_if;
    `; 
    //replaced with LUT above
    //thresh := ((10*135) / ((i / iSet) - 1));
}
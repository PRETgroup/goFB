basicFB BfbIDMTCurve;

interface of BfbIDMTCurve {
	in event tick;
    in event i_measured;
    in event iSet_change;

    out event unsafe;

    in with i_measured udint i;
    in with iSet_change udint iSet;    
} 

architecture of BfbIDMTCurve {
	internals {
        ulint v := 0;
        ulint thresh := 0;
        udint K := 10000;	
		udint B := 135;
    }
    
    states {
		s_start {
			-> s_wait;
		}

		s_wait {
			run in "ST" `v := 0;`;
			-> s_count on i > iSet;
		}

        s_count {
            run updateThresh;
            run in "ST" `v := v + 1;`;
            
            -> s_wait on i <= iSet;
            -> s_over on v > thresh;
            -> s_count on tick;
        }

        s_over {
            emit unsafe;
            -> s_wait on i <= iSet;
            -> s_over;
        }
	}

    algorithm updateThresh in "ST" `
        thresh := ((K*B) / ((i / iSet) - 1));
    `;
}